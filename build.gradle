/*
 * Copyright (c) 2021-2024 tracetronic GmbH
 *
 * SPDX-License-Identifier: BSD-3-Clause
 */

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'idea'
    id 'groovy'
    id 'jacoco'
    id 'codenarc'
    id 'maven-publish'
    id 'org.jenkins-ci.jpi' version "$jpiVersion"
    id "org.openapi.generator" version "$openapiVersion"
    id "com.github.spotbugs" version "$spotBugsPlugin"
    id 'org.cyclonedx.bom' version "$cyclonedxVersion"
    id 'com.github.jk1.dependency-license-report' version '2.9'
    id "org.owasp.dependencycheck" version "$depCheckVersion"
}

group = 'de.tracetronic.jenkins.plugins'
version = project.properties['version']
description = 'This plugin enables a platform-independent test execution. Due to the reduced complexity and ' +
        'communication via REST API, ' +
        'it provides an easy and reliable entry into continuous testing. It integrates ' +
        'ecu.test with Jenkins and allows report generation and its upload of ' +
        'automated test execution.'

jenkinsPlugin {
    jenkinsVersion = "${project.properties['jenkinsVersion']}"
    shortName = 'ecu-test-execution'
    group = "${group}"
    displayName = 'ecu.test execution plugin'

    url = 'https://plugins.jenkins.io/ecu-test-execution/'
    gitHub = new URI('https://github.com/jenkinsci/ecu-test-execution-plugin')

    generateTests = false
    configurePublishing = false
    extension = 'hpi'
}

repositories {
    mavenCentral()
}

dependencies {
    // provides to correct versions of typical needed plugins
    implementation platform('io.jenkins.tools.bom:bom-2.426.x:3041.ve87ce2cdf223')

    implementation 'org.jenkins-ci.plugins:credentials'
    implementation 'org.jenkins-ci.plugins:plain-credentials'
    implementation 'org.jenkins-ci.plugins:script-security'
    implementation 'org.jenkins-ci.plugins:structs'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-step-api'

    // codenarc
    codenarc('org.codenarc:CodeNarc:3.5.0')
    codenarc('org.codehaus.groovy:groovy-all:3.0.24')

    // ecu.test-java-rest-client
    api 'io.swagger:swagger-annotations:1.6.14'
    api 'com.google.code.gson:gson:2.11.0'
    api 'io.gsonfire:gson-fire:1.9.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    api 'org.apache.commons:commons-lang3:3.17.0'
    api 'javax.annotation:javax.annotation-api:1.3.2'
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    api 'org.slf4j:slf4j-api:2.0.16'

    // test
    testImplementation 'io.swagger:swagger-parser:1.0.72'

    // sandbox 1.34 seems to be incompatible with jenkins 2.426.3
    testImplementation('org.kohsuke:groovy-sandbox') { version { strictly '1.33'}}
    testImplementation('org.jenkins-ci.plugins.workflow:workflow-basic-steps') {//:1042.ve7b_140c4a_e0c') {
        exclude group: 'org.jenkins-ci.modules', module: 'instance-identity'
    }
    testImplementation 'org.jenkins-ci.plugins:http_request'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-cps'
    testImplementation('org.jenkins-ci.plugins.workflow:workflow-cps') {artifact{ classifier = 'tests'}}
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-job'
    testImplementation('org.jenkins-ci.plugins.workflow:workflow-step-api') {artifact{ classifier = 'tests'}}
    testImplementation('org.jenkins-ci.plugins.workflow:workflow-support') {artifact{ classifier = 'tests'}}
    testImplementation 'org.jenkins-ci.plugins:credentials-binding'
    testImplementation 'org.jenkins-ci.plugins:http_request'
    testImplementation "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    testImplementation('org.testcontainers:spock:1.20.4') {
        exclude group: 'org.spockframework', module: 'spock-core'
    }
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'org.spockframework:spock-junit4:2.3-groovy-3.0'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.objenesis:objenesis:3.4'
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava') {
            /* org.jenkins-ci.main:jenkins-core:2.426.3 use com.google.guava:guava:32.1.1-jre
             * which has dependency issues https://github.com/google/guava/issues/6657
             */
            details.useVersion '33.2.0-jre'
        }
    }
}

// jenkins-bom is added by the jpi plugin since version 0.46, but creates issues with the spockframework
configurations.testImplementation {
    exclude group: 'org.jenkins-ci.main', module: 'jenkins-bom'
}

// remove at runtime, as it provides a not fully functional SAXParser which makes integration tests fail
configurations.testRuntimeOnly {
    exclude group: 'pull-parser', module: 'pull-parser'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "ecu-test-execution"
            groupId = "${group}"
            version = "${version}"

            from components.java

            pom {
                packaging = "$jenkinsPlugin.extension"
                name = "$jenkinsPlugin.displayName"
                description = "$project.description"
                url = 'https://plugins.jenkins.io/ecu-test-execution/'

                scm {
                    url =  'https://github.com/jenkinsci/ecu-test-execution-plugin'
                    connection = 'scm:git:git://github.com/jenkinsci/ecu-test-execution-plugin.git'
                    developerConnection = 'scm:git:git@github.com:jenkinsci/ecu-test-execution-plugin.git'
                    tag = "ecu-test-execution-$project.version"
                }

                developers {
                    developer {
                        id = 'TT-S3-D1'
                        name = 'Sebastian Dietze'
                        email = 'sebastian.dietze@tracetronic.de'
                    }
                    developer {
                        id = 'MartinGroscheTT'
                        name = 'Martin Grosche'
                        email = 'martin.grosche@tracetronic.de'
                    }
                    developer {
                        id = 'ErikRehmTT'
                        name = 'Erik Rehm'
                        email = 'erik.rehm@tracetronic.de'
                    }
                }

                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                        distribution = 'repo'
                        comments = 'All source code is licensed under the BSD license.'
                    }
                }

                contributors {
                    contributor {
                        url = 'https://github.com/jenkinsci/ecu-test-execution-plugin/graphs/contributors'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/jenkinsci/ecu-test-execution-plugin/issues'
                }
                ciManagement {
                    system = 'Jenkins'
                    url = 'https://ci.jenkins.io/job/plugins/job/ecu-test-execution-plugin/'
                }
            }
        }
    }
    repositories {
        maven {
            def path = project.version.toString().endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
            name 'Jenkins'
            url "https://repo.jenkins-ci.org/${path}"
            credentials {
                username = project.findProperty('jenkinsUser')
                password = project.findProperty('jenkinsPwd')
            }
        }
        maven {
            name = "Artifacts"
            url = uri("file://${buildDir}/artifacts")
        }
    }
}

tasks.register('cleanJenkins', Delete) {
    delete 'work'
}

tasks.register('cleanApi', Delete) {
    delete 'generated'
}
clean.dependsOn cleanApi

tasks.named('server').configure {
    it.port.set('8082')
}

test {
    finalizedBy jacocoTestReport

    dependsOn 'unitTest'
    dependsOn 'integrationTest'
}

tasks.register('unitTest', Test) {
    filter {
        excludeTestsMatching '*ContainerTest'
        excludeTestsMatching '*IT'
    }
    useJUnitPlatform()

    // skip some process tests for ci
    systemProperties('spock.skip.sandbox': System.getProperty('spock.skip.sandbox', 'true'))
}

jacoco {
    toolVersion = "0.8.11" // Java 17 and 21
}

tasks.register('integrationTest', Test) {
    // exclude code, which can not be instrumented correctly
    jacoco {
        excludes = [
                "com/gargoylesoftware/**",
                "org/htmlunit/**"
        ]
    }

    filter {
        excludeTestsMatching '*Test'
    }
    useJUnitPlatform()

    // skip some process tests for ci
    systemProperties('spock.skip.sandbox': System.getProperty('spock.skip.sandbox', 'true'))
}

tasks.register('containerTest', Test) {
    doFirst {
        def propFile = "src/test/resources/containerTest.properties"
        assert file(propFile).exists(): "No property file '$propFile' for container test found"

        def containerProps = new Properties()
        file(propFile).withInputStream {
            containerProps.load(it)
        }
        containerProps.each {
            environment it.key, it.value
        }
    }

    filter {
        includeTestsMatching('*ContainerTest')
    }
    useJUnitPlatform()
}

tasks.withType(GroovyCompile).configureEach {
    groovyOptions.optimizationOptions.indy = true
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

spotbugs {
    ignoreFailures = true
}

spotbugsMain {
    projectName = project.name
    release = version
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
    reports {
        xml.required = true
        html.required = true
    }
}

codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
    configFile = file('config/codenarc/rules.groovy')
    ignoreFailures = true
}

codenarcTest {
    enabled = false
    compilationClasspath = codenarcMain.compilationClasspath + sourceSets.test.compileClasspath + sourceSets.test.output
    configFile = file('config/codenarc/rules-test.groovy')
    ignoreFailures = true
}

tasks.register('openApiGenerateV1', GenerateTask) {
    inputSpec = "$rootDir/specs/openapi_v1.yml".toString()
    generatorName = "java"
    id = 'ecu-test-rest-java-client_v1'
    version = project.version
    groupId = 'de.tracetronic.cxs'
    outputDir = "$rootDir/generated/v1".toString()
    packageName = "de.tracetronic.cxs.generated.et.client.v1"
    apiPackage = 'de.tracetronic.cxs.generated.et.client.api.v1'
    invokerPackage = 'de.tracetronic.cxs.generated.et.client.v1'
    modelPackage = 'de.tracetronic.cxs.generated.et.client.model.v1'
    library = 'okhttp-gson'
    generateModelTests = false
    generateApiTests = false
    configOptions = [
            licenseName: "MIT License",
            licenseUrl: "https://spdx.org/licenses/MIT.html",
            dateLibrary: "java${project.properties['javaVersion']}"
    ]
}

tasks.register('openApiGenerateV2', GenerateTask) {
    inputSpec = "$rootDir/specs/openapi_v2.yml".toString()
    generatorName = "java"
    id = 'ecu-test-rest-java-client_v2'
    version = project.version
    groupId = 'de.tracetronic.cxs'
    outputDir = "$rootDir/generated/v2".toString()
    packageName = "de.tracetronic.cxs.generated.et.client.v2"
    apiPackage = 'de.tracetronic.cxs.generated.et.client.api.v2'
    invokerPackage = 'de.tracetronic.cxs.generated.et.client.v2'
    modelPackage = 'de.tracetronic.cxs.generated.et.client.model.v2'
    library = 'okhttp-gson'
    generateModelTests = false
    generateApiTests = false
    configOptions = [
            licenseName: "MIT License",
            licenseUrl: "https://spdx.org/licenses/MIT.html",
            dateLibrary: "java${project.properties['javaVersion']}"
    ]
}

licenseReport {
    outputDir = "$projectDir/build/licenses"
    projects = [project] + project.subprojects
    configurations = ['runtimeClasspath']
    excludeGroups = []
    excludes = []
    excludeOwnGroup = false
    excludeBoms = false
    renderers = []
    importers = []
    allowedLicensesFile = new File("$projectDir/config/allowed_licenses.json")
}

cyclonedxBom {
    outputFormat = "json"
}

dependencyCheck {
    autoUpdate = false
    formats = ['HTML', 'JSON']
    failBuildOnCVSS = Float.parseFloat(project.properties['failBuildOnCVSS']) // only "HIGH" and "CRITICAL" vulnerabilities
    suppressionFile = 'config/vulnerabilities/suppressionFile.xml'
    nvd.apiKey = project.findProperty("nvdApiKey") ?: ""
}

tasks.register('filterVulnerabilityJson', FilterTask) {
    minimumScore = Float.parseFloat(project.properties['failBuildOnCVSS'])
}

tasks.register('dependencyCheckEval') {
    it.dependsOn dependencyCheckAnalyze finalizedBy filterVulnerabilityJson
}

sourceSets {
    main {
        java {
            srcDir(files("${openApiGenerateV1.outputDir.get()}/src/main/java"))
            srcDir(files("${openApiGenerateV2.outputDir.get()}/src/main/java"))
        }
    }
}

tasks.register('generateApi') { dependsOn openApiGenerateV1, openApiGenerateV2 }
tasks.compileJava.dependsOn generateApi

idea {
    project {
        vcs = 'Git'
        jdkName = "${project.properties['javaVersion']}"
        languageLevel = "${project.properties['javaVersion']}"
    }
    module {
        jdkName = "${project.properties['javaVersion']}"
        downloadJavadoc = true
        downloadSources = true
    }
}

wrapper {
    gradleVersion = "${project.properties['gradleVersion']}"
}
